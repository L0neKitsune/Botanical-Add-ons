buildscript {
    ext.kotlin_version = '0.14.451'
    repositories {
        mavenCentral()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:1.2-SNAPSHOT'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

repositories {
    maven {
        name = "chickenbones"
        url = "http://chickenbones.net/maven/"
    }
    mavenCentral()
}

apply plugin: 'forge'
apply plugin: 'kotlin'
apply plugin: 'curseforge'

ext.configFile = file('build.properties')

ext.config = parseConfig(configFile)

version = "${config.version}-${config.build_number}"
group= "ninja.shadowfox.botanical_addons" // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = config.mod_name

minecraft {
    version = "1.7.10-10.13.4.1448-1.7.10"
    runDir = "eclipse"

    version = "${config.mc_version}-${config.forge_version}"

    replace 'GRADLE:BUILD', config.build_number
    replace 'GRADLE:VERSION', config.version
    replaceIn 'LibMisc.java'
}

dependencies {
    compile files('Baubles-deobf-1.7.10-1.0.1.10.jar')
    compile files('Botania r1.7-224-deobf.jar')
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
}

processResources {
    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'

        // replace version and mcversion
        expand 'version': project.version, 'mcversion': project.minecraft.version
    }

    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info', '**/psd/**'
    }
}

def parseConfig(File config) {
    config.withReader {
        def prop = new Properties()
        prop.load(it)
        return (new ConfigSlurper().parse(prop))
    }
}

idea { module { inheritOutputDirs = true } }
sourceSets {
    main.java.srcDirs += 'src/main/kotlin'
}
